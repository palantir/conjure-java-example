buildscript {
    repositories {
        repositories {
            mavenCentral()
            maven {
                url 'https://dl.bintray.com/palantir/releases/'
            }
            maven {
                url 'https://plugins.gradle.org/m2/'
            }
        }
    }

    dependencies {
        classpath 'com.netflix.nebula:nebula-dependency-recommender:5.2.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.20.1'
        classpath 'com.palantir.launchconfig:gradle-launch-config-plugin:1.2.0'
        classpath 'com.palantir.configurationresolver:gradle-configuration-resolver-plugin:0.3.0'
        classpath 'com.palantir.gradle.conjure:gradle-conjure:4.2.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.7.1'
        classpath 'gradle.plugin.com.palantir:gradle-circle-style:1.1.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:1.2.15'
    }
}

apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.circle.style'

dependencies {
    baseline 'com.palantir.baseline:gradle-baseline-java-config:0.20.1@zip'
}

allprojects {
    apply plugin: 'com.palantir.configuration-resolver'
    apply plugin: 'nebula.dependency-recommender'

    group 'com.palantir.conjure.examples'
    // TODO: clarify error message
    // workaround for compileConjureTypescript Error: Expected version to be valid SLS version but found "b02eaaf.dirty"
    version "0.1.0"

    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
    }

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('versions.props')
    }
}

configure(subprojects - project(':example-api')) {
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-error-prone'
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-XepDisableWarningsInGeneratedCode', '-Werror']
    }
}
